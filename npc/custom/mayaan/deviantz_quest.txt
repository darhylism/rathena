mall01,100,135,4	script	Deviantz Quest	4_M_OPERATION,{
	mes "[Deviantz]";
	mes "I come from Evil Wonderland to aid you.";
	mes "I can make you special itemz!.";
	next;
	switch(select("Fallen Angel Wing:Archangel Wing:Kirin Armor:Etran Shirt:Gyges Ring:Close")) {
		case 1:
			mes "[Deviantz]";
			mes "^3355FFFallen Angel Wing^000000.";
			setarray .@Items[0],2589,6393,100,6394,100,2255,5,2890,5,2891,5,2527,1,2515,1,6091,3,22516,300,6100,100;
			break;
		case 2:
			mes "[Deviantz]";
			mes "^3355FFArchangel Wing^000000.";
			setarray .@Items[0],2573,6393,100,6394,100,2254,5,2890,5,2891,1,2527,1,2515,1,2513,1,7165,200;
			break;
		case 3:
			mes "[Deviantz]";
			mes "^3355FFKirin Armor^000000.";
			setarray .@Items[0],15103,7510,10,6022,10,7562,10,7451,5,7450,5,15051,1,1631,1,15090,1,2001,1,13062,1,4054,1;
			break;
		case 4:
			mes "[Deviantz]";
			mes "^3355FFEtran Shirt^000000.";
			setarray .@Items[0],20797,7443,100,2554,1,2509,5,2590,5,20718,5,2544,5,20721,5,4054,1,4174,1,4047,1,4241,1;
			break;
		case 5:
			mes "[Deviantz]";
			mes "^3355FFGyges Ring^000000.";
			setarray .@Items[0],2985,7799,300,2898,10,969,10,2610,10;
			break;
		default:
			close;
	}
	
	mes "^FF0000The items I need are as follows:^000000";
	set .@itemmissing,0;
	for(set .@i,1; .@i<getarraysize(.@Items); set .@i,.@i+2) {
		set .@amount,.@Items[.@i+1];
		set .@id,.@Items[.@i];
		mes " ~ "+.@amount+"x "+((countitem(.@id)<.@amount)?("^dd3355"):("^33dd55"))+getitemname(.@id)+((getitemslots(.@id)>0)?" ["+getitemslots(.@id)+"]":"")+"^000000";
		if (countitem(.@id) < .@amount) 
			set .@itemmissing,1;
	}
	if (.@itemmissing) {
		next;
		mes "[Deviantz]";
		mes "Go on and find all these items.";
		mes "I will wait here to aid you.";
		close;
	}
	next;
	mes "[Deviantz]";
	mes "Do you desire ^3355FF"+getitemname(.@Items[0])+"^000000?";
	next;
	if(select("No","Yes")==1) {
		mes "[Deviantz]";
		mes "Please return if you change your mind.";
		close;
	}
	mes "[Deviantz]";
	mes "Here is your ^3355FF"+getitemname(.@Items[0])+"^000000.";
	mes "May it serve you well.";
	//second check if the player tries to bypass the first
	for(set .@i,1; .@i<getarraysize(.@Items); set .@i,.@i+2) {
		set .@amount,.@Items[.@i+1];
		set .@id,.@Items[.@i];
		if (countitem(.@id) < .@amount) 
			close;
	}
	// Delete the items _after_ the second check
	for(set .@i,1; .@i<getarraysize(.@Items); set .@i,.@i+2) {
		set .@amount,.@Items[.@i+1];
		set .@id,.@Items[.@i];
		delitem .@id, .@amount;
	}
	getitem .@Items[0],1;
	close;
}
